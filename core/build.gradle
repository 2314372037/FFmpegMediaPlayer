apply plugin: 'com.android.library'
apply from: "$rootProject.projectDir/publishing.gradle"

android {
    namespace 'wseemann.media'

    compileSdk versions.get("compileSdkVersion")

    defaultConfig {
        minSdkVersion versions.get("minSdkVersion")
        targetSdkVersion versions.get("targetSdkVersion")
        versionCode versions.get("versionCode")
        versionName versions.get("versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant('release')
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.wseemann'
                artifactId = 'FFmpegMediaPlayer-core'
                version = "$project.android.defaultConfig.versionName"

                pom {
                    name = 'FFmpegMediaPlayer-core'
                    description = 'FFmpegMediaPlayer library'
                    url = 'https://github.com/wseemann/FFmpegMediaPlayer'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'wseemann'
                            name = 'William Seemann'
                        }
                    }
                    scm {
                        connection = 'scm:hg:https://github.com/wseemann/FFmpegMediaPlayer'
                        developerConnection = 'scm:hg:https://github.com/wseemann/FFmpegMediaPlayer'
                        url = 'https://github.com/wseemann/FFmpegMediaPlayer'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
